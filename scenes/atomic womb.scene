scene 'Atomic womb'

sampler
    adaptive(10, 0.001, 2, 6);

camera
    perspective([0, 4, -8], [0, 1.75, 0.5], ^Y, 65, 640, 480);

background
    sky;

ambient
    0.2;

lights
    spot(4^Y, ^0, 40, 120, white);
    point(+20, 30, 5, 0.1);
    point(-20, 30, 5, 0.1);
    spheric(
        x: 0.0, y: -50.0, z: -50.0,
        radius: 10.0,
        brightness: 0.35,
        samples: 12);

objects

    set marble01 = marble(white, gainsboro, 2, 0.7, 6, 0, 0.4, 5);
    set marble02 = marble(white, rgb(0.720, 0.725, 0.510), 1.1, 0.3, 5, 0.0, 0.4, 5);

    set obj1 = difference(
        box(-1, 0, -1, 1, 2, 1, marble02),
        sphere(0, 1, 0, 1.3, marble02),
        marble02)
        spin 45^Y
        spin -15^X
        move 0.6^Y;

    set obj2 = difference(
        box(-1, -1, -1, 1, 1, 1, marble01),
        sphere(0, 0, 0, 1.3, marble01),
        marble01)
        spin 45^Z
        spin -15^X
        move ^Y
        size 2;

    obj1, obj2;

    difference(
        box(-1, -1, -1, 1, 1, 1, marble01),
        sphere(^0, 1.3, marble01),
        marble01)
        move ^Y
        size [6, 8, 7]
        move 2^Z;

    union(
        obj2 move [-19.5, 4.0, 30],
        obj1 move [-19.5, 4.0, 30]);

    union(
        obj2 move [+19.5, 4.0, 30],
        obj1 move [+19.5, 4.0, 30]);
end.
