scene Seascape;

sampler
    adaptive(25, 0.0001, 3, 6);

camera
    // A camera travel:
    perspective(
        location: var([+4, +5, -8], 0.70: [-3, 0.05, -1.25], 0.85: [-3.0, 0.05, -1.25], [-1.5, 0.05, -1.25]),
        target:   var([-2, +1,  0], 0.70: [-5, 0.07, -1.25], 0.85: [-4.5, 0.50, -0.50], [-4.0, 1.00, -0.50]),
        up:       ^Y,
        angle:    var(65, 0.70: 45, 0.85: 55, 55),
        width:    640,
        height:   480);

background
    sky;

ambient
    // Try ambient occlusion!
    0.10;

lights
    point([+1000, 1000, -1000], white);

media
    fog(rgb(0.55, 0.77, 0.85), 300, 10, 0);

objects

    set cap(m) = pyramid(2, 1, m) size [1, 1, 0.5] spin -90^Z move 2^X;

    set cube(m) = union(
        difference(
            box([-2.0, -1.0, -0.5], [+2.0, +1.0, +0.5], m),
            cylinder([-1.6, 0.0, -2.0], [-1.6, 0.0, +2.0], 0.3, m),
            cylinder([+1.6, 0.0, -2.0], [+1.6, 0.0, +2.0], 0.3, m)),
        sphere(-1.6, 0.0, -0.5, 0.15, plastic(red)),
        sphere(-1.6, 0.0,  0.0, 0.10, plastic(red)),
        sphere(-1.6, 0.0, +0.5, 0.15, plastic(red)),
        sphere(+1.6, 0.0, -0.5, 0.15, plastic(red)),
        sphere(+1.6, 0.0,  0.0, 0.10, plastic(red)),
        sphere(+1.6, 0.0, +0.5, 0.15, plastic(red)),
        cap(m),
        cap(m) spin 180^z);

    set pcube(m) = union(
        difference(
            box([-2.0, -1.0, -0.5], [+2.0, +1.0, +0.5], m),
            cylinder([-1.6, 0.0, -2.0], [-1.6, 0.0, +2.0], 0.3, m),
            cylinder([+1.6, 0.0, -2.0], [+1.6, 0.0, +2.0], 0.3, m),
            box([+1.35, -1.05, 0.10], [+1.15, +1.05, -0.10], m),
            box([-1.35, -1.05, 0.10], [-1.15, +1.05, -0.10], m)),
        sphere(-1.6, 0.0, -0.5, 0.15, plastic(red)),
        sphere(-1.6, 0.0,  0.0, 0.10, plastic(red)),
        sphere(-1.6, 0.0, +0.5, 0.15, plastic(red)),
        sphere(+1.6, 0.0, -0.5, 0.15, plastic(red)),
        sphere(+1.6, 0.0,  0.0, 0.10, plastic(red)),
        sphere(+1.6, 0.0, +0.5, 0.15, plastic(red)),
        cap(m),
        cap(m) spin 180^z);

    set building(mat) = union(
        cube(mat),
        cube(mat)  spin 90^x + 90^z,
        pcube(mat) spin 90^x + 90^y);

    set grad(fromColor, toColor) = gradient(fromColor, -3^Y, toColor, +3^Y);

    building(grad(slateBlue,   gainsboro))    move [  0, 0,  0];
    building(grad(forestGreen, midnightBlue)) move [  0, 0,  6];
    building(grad(slateBlue,   gainsboro))    move [  0, 0, 12];
    building(grad(forestGreen, midnightBlue)) move [ -6, 0,  0];
    building(grad(slateBlue,   gainsboro))    move [ -6, 0,  6];
    building(grad(forestGreen, midnightBlue)) move [ -6, 0, 12];
    building(grad(slateBlue,   silver))       move [-12, 0,  0];
    building(grad(gold,        darkGreen))    move [-12, 0,  6];
    building(grad(slateBlue,   silver))       move [-12, 0, 12];

    set bridge = union(
        box([-5.01, -0.10, -1.36], [-0.99, -0.09, -1.14], metal(darkGray, 0.1)),
        cylinder([-5, 0, -1.35], [-1, 0, -1.35], 0.02, metal(silver, 0.1)),
        cylinder([-5, 0, -1.15], [-1, 0, -1.15], 0.02, metal(silver, 0.1)),
        sphere([-5, 0, -1.35], 0.02, metal(silver, 0.1)),
        sphere([-1, 0, -1.35], 0.02, metal(silver, 0.1)),
        sphere([-5, 0, -1.15], 0.02, metal(silver, 0.1)),
        sphere([-5, 0, -1.15], 0.02, metal(silver, 0.1)),
        cylinder([-4.90, -0.099, -1.35], [-4.90, 0.00, -1.35], 0.01, plastic(darkGray))
            loop 40 by 0.1^X,
        cylinder([-4.90, -0.099, -1.15], [-4.90, 0.00, -1.15], 0.01, plastic(darkGray))
            loop 40 by 0.1^X);

    bridge move [ 0, 0, 0.0];
    bridge move [ 0, 0, 2.5];
    bridge move [ 0, 0, 6.0];
    bridge move [ 0, 0, 8.5];
    bridge move [ 0, 0, 12];
    bridge move [ 0, 0, 14.5];
    bridge move [-6, 0, 0.0];
    bridge move [-6, 0, 2.5];
    bridge move [-6, 0, 6.0];
    bridge move [-6, 0, 8.5];

    set balcony = union(
        cylinder([-5.40, 1.00, -0.49], [-5.40, 1.09, -0.49], 0.01, plastic(darkGray))
            loop 14 by 0.1^X,
        cylinder([-5.40, 1.00, +0.49], [-5.40, 1.09, +0.49], 0.01, plastic(darkGray))
            loop 14 by 0.1^X,
        cylinder([-4.01, 1.00, -0.49], [-4.01, 1.09, -0.49], 0.01, plastic(darkGray))
            loop 10 by 0.1^Z,
        cylinder([-5.50, 1.10, -0.49], [-4.00, 1.10, -0.49], 0.02, metal(silver, 0.1)),
        cylinder([-5.50, 1.10, +0.49], [-4.00, 1.10, +0.49], 0.02, metal(silver, 0.1)),
        cylinder([-4.00, 1.10, -0.49], [-4.00, 1.10, +0.49], 0.02, metal(silver, 0.1)),
        sphere([-4.00, 1.10, -0.49], 0.02, metal(silver, 0.1)),
        sphere([-4.00, 1.10, +0.49], 0.02, metal(silver, 0.1)),
        box([-5.50, 1.000, -0.49], [-4.00, 1.005, +0.49], spotted(lawnGreen, forestGreen)));

    union(
        cylinder([-5.00, 1.12, 0.15], [-5.00, 1.14, 0.15], 0.15,
            wood(brown, rgb(0.74, 0.20, 0.20), 2, 3)),
        cylinder([-5.00, 1.005, 0.15], [-5.00, 1.12, 0.15], 0.03,
            metal(darkGray, 0.2)));

    union(
        box([-4.80, 1.07, 0.18], [-4.20, 1.10, 0.28],
            wood(brown, rgb(0.74, 0.20, 0.20), 2, 3)),
        cylinder([-4.75, 1.005, 0.23], [-4.75, 1.07, 0.23], 0.015,
            metal(darkGray, 0.2)),
        cylinder([-4.25, 1.005, 0.23], [-4.25, 1.07, 0.23], 0.015,
            metal(darkGray, 0.2)));

    balcony;
    balcony move +6^X;
    balcony move -6^X;
    balcony move +6^Z;
    balcony move +6^X + 6^Z;
    balcony move -6^X + 6^Z;
    balcony move +12^Z;
    balcony move +6^X + 12^Z;
    balcony move -6^X + 12^Z;

    plane([0,1,0.001], -5,
        pigment(
            layers(
                gradient(-0.5^Z, white, 6^Z+^X, darkBlue),
                0.7,
                spots(royalBlue, aqua, 1, 3)),
        0.5, 0, 0, 0.0002, bumps(0.03, 0.3)));
end.

